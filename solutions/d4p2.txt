@name Advent of Code/D4P2
@persist [LineId Sum Loaded Counted]:number [Lines CardScores CardQuantites]:array
@strict

interval(10)

event fileLoaded(_:string, Data:string) {
    Lines = Data:explode("\n")
    CardScores = array()
    CardQuantites = array()
    LineId = 1
    Loaded = 1
    Sum = 0
}

if (first()) {
    fileLoad("advent_of_code/d4.txt")
    
} elseif (Loaded) {
    while (LineId <= Lines:count() & perf(75)) {
        local Line = Lines[LineId, string]:trim():explode(":")[2, string]
        
        local WinNumbers = Line:explode("|")[1, string]:trim():explode(" ")
        local MyNumbers = Line:explode("|")[2, string]:trim():explode(" ")
        local WinTable = table()

        local Score = 0

        foreach (_:number, WinNumber:string = WinNumbers) {
            WinTable[WinNumber:trim(), number] = 1
        }

        foreach (_:number, MyNumber:string = MyNumbers) {
            if (WinTable[MyNumber:trim(), number] & MyNumber:trim() != "") {
                Score++         
            }        
        }        
    
        CardScores[LineId, number] = Score
        CardQuantites[LineId, number] = 1
        LineId++         
    }
    
    if (LineId > Lines:count()) {
        LineId = 1
        Counted = 1
        Loaded = 0
        
    }
     
} elseif (Counted) {
    while (LineId <= Lines:count() & perf(75)) {
        for (I = LineId + 1, LineId + CardScores[LineId, number], 1) {
            CardQuantites[I, number] = CardQuantites[I, number] + CardQuantites[LineId , number]   
        }

        Sum += CardQuantites[LineId , number] 

        LineId++         
    }
    
    if (LineId > Lines:count()) {print(Sum) selfDestruct()} 
}
